module ppm-to-fast

imports
   libstratego-lib
   FAST
   ofp-simplify

signature
  constructors

  ALLOCATED : ALLOCATED
  INTERNAL  : INTERNAL

  clMemObject : symbol -> clMemObject


strategies //=================START OF STRATEGIES============================

  io-ppm-to-fast =
    io-wrap(ppm-to-fast)

  ppm-to-fast =
{ st,lt,save
        :  ?Program(units)
        ;  new-hashtable => st
        ;  new-hashtable => lt
        ;  !Program(units)
// 1. get implicit suffix list [(type,suffix)]
// 2. get var list with (type,suffix) -> hashtable lt -> ("var", (type,suffix))
// 3. add type declaration for each var in lt in a scope
// ? only traverse scopes, creating hashtable and then adding decls ?
        ;  topdown(try(fast-get-symbols(|st)))
//        ;  topdown(try(ppm-get-symbols(|st,lt)))
//        ;  bottomup(try(ppm-add-cl(|st,lt)))
//        ;  topdown(try(fast-flatten-scope-parts))
//        ;  bottomup(try(caf-lower-ast))  => save
        ;  !"--------------------------------------------------------------------------------------"
        ;  debug()
//        ;  !save
}


rules //========================START OF RULES===============================

// Local thread group size
// TODO - make dependent on dim and device
opencl-local-size =
     ![IntVal("16"),IntVal("8"),IntVal("1")]

opencl-arg-shape(|st) =
     ?PartRef(symbol,no-section-subscripts(),_)
  ;  <hashtable-get(|symbol)> st
  ;  ?(type,attrs)
  ;  <fetch-elem(?Dimension(shape))> attrs
  ;  <map(fast-dim-size)> shape
 <+  ![1,1,1]

opencl-rank(|st) =
     ?args
  ;  <map(opencl-arg-rank(|st))> args
  ;  <foldr(!1,max)> <id>

opencl-arg-rank(|st) =
     ?PartRef(symbol,no-section-subscripts(),_)
  ;  <hashtable-get(|symbol)> st
  ;  ?(type,attrs)
  ;  <fetch-elem(?Dimension(shape))> attrs
  ; <length> shape
 <+  !1

// FAST strategies (MOVE TO fortran/trans)
// ---------------

fast-var-name =
     ?VarRef(name)        ;  !name
 <+  ?VarDef(name)        ;  !name

// Remove excess list elements inside of a Scope
//   - occurs when a single statement is replaced by a list of statements
fast-flatten-scope-parts =
     ?Scope(decls,execs,funcs)
  ;  !Scope(<flatten-list>decls, <flatten-list>execs, <flatten-list>funcs)

fast-allocation-object =
     ?Allocation(object,_,_)
  ;  <fast-var-name> object

// assume literals for now, otherwise get const from symbol table
// TODO - use choice to use lb info
fast-build-allocation-size(|st,obj) =
     ?AllocateShapeSpec(no-lower-bound(),ub)
  ;  !ub

fast-dim-size =
     ?Range(lb,ub)
  ;  !Parens(Minus(ub,Parens(lb)))
  ;  !Parens(Plus(<id>,"1"))

// Size of a type in bytes
// TODO - add other intrinsic types and support for kinds
// TODO - could replace kind with size in bytes!!!!
fast-type-size =
     ?REAL()              ;  !"4"
 <+  ?INTEGER()           ;  !"4"

fast-mult-op =
     ?(l,r)
  ;  !Mult(l,r)

fast-allocation-size(|st) =
     ?symbol
  ;  <hashtable-get(|symbol)> st
  ;  ?(type,attrs)
  ;  <fetch-elem(?Dimension(shape))> attrs
  ;  <map(fast-dim-size)> shape
  ;  <foldr(!"1",fast-mult-op)> <id>
  ;  <fast-mult-op> (<fast-type-size>type,<id>)

fast-fix-no-lower-bound =
     ?no-lower-bound()
  ;  !"1"

fast-update-allocation-dim =
     ?(Range(lb,ub),Range(_,_))
  ;  !Range(lb,ub)

fast-update-allocation-codim =
     ?(Range(lb,ub),Range(_,_))
  ;  !Range(lb,ub)

// Update the symbol table with information from an allocate statement
fast-update-allocation-symbol(|st) =
{shape,coshape,attrs
  :  topdown(try(fast-fix-no-lower-bound))
  ;  ?Allocation(VarRef(obj),alloc_shape,alloc_coshape)
  ;  <hashtable-get(|obj)> st
  ;  ?(type,decl_attrs)
  ;  <elem> (ALLOCATABLE(),decl_attrs)   // ensure this symbol is allocatable
  ;  <fetch-elem(?Dimension(decl_shape))> decl_attrs
  ;  <fetch-elem(?Codimension(decl_coshape))> decl_attrs
  ;  <zip(fast-update-allocation-dim)>   (alloc_shape,  decl_shape)   =>   shape
  ;  <zip(fast-update-allocation-codim)> (alloc_coshape,decl_coshape) => coshape
  ;  <filter( try(?Dimension(_)   ; !Dimension(shape))     )> decl_attrs
  ;  <filter( try(?Codimension(_) ; !Codimension(coshape)) )> <id>
  ;  <filter( try(?ALLOCATABLE()  ; !ALLOCATED()) )> <id> => attrs
  ;  <hashtable-put(|obj,(type,attrs))> st
}

fast-build-use-stmt =
     ?module_name
  ;  !UseStmt(no-label(), no-module-nature(), module_name, [])

fast-build-type-decl(|type,attrs) =
     ?object_name
  ;  !TypeDeclarationStmt(  no-label()
                          , type
                          , attrs
                          , [Var(object_name,type,no-init())]
                         )

fast-build-block(|decls,execs) =
     !Block(no-label()
          , no-name()
          , Scope(decls,execs,[])
          , EndBlockStmt(no-label(),no-name()))

// Allocate memory on OpenCL device via call to createBuffer()
opencl-build-create-buffer(|st,device) =
     ?obj
  ;  <fast-allocation-size(|st)> obj
  ;  ![  AssignmentStmt(no-label(),VarDef("cl_size__"),<id>)
       , AssignmentStmt(no-label(),<cl-wrap-name> obj
          , FunctionReference("createBuffer"
             , [device,"CL_MEM_READ_WRITE",VarRef("cl_size__")
               ,"C_NULL_PTR"]))
      ]

// Deallocate memory on OpenCL device via call to releaseMemObject()
opencl-build-release-buffer(|st,device) =
    !AssignmentStmt(no-label(),VarDef("cl_status__")
          , FunctionReference("releaseMemObject", [<cl-wrap-var> <id>]))

// Create an OpenCL kernel object via call to createKernel
//TODO - check selector against device to ensure they refer to same image
opencl-build-create-kernel(|ct,device) =
     ?obj
  ;  <hashtable-get(|obj)> ct
  ;  ?(kernel,DerivedType("CLKernel",[]),[]) {annotations}
  ;  !AssignmentStmt(no-label(),kernel
         , FunctionReference("createKernel",[<cl-wrap-name>device,<write-to-string>obj]))
  ;  ![<id>]
 <+  ![    ]

// Create an OpenCL kernel object via call to createKernel
//TODO - check selector against device to ensure they refer to same image
opencl-build-set-kernel-args(|ct,device) =
{kernel_arg_funcs
  :  ?(obj,args)
  ;  <hashtable-get(|obj)> ct
  ;  ?(kernel,DerivedType("CLKernel",[]),[]) {annotations}
//  ;  <lookup> ("args", annotations)
  ;  <map(opencl-build-set-kernel-arg(|ct,kernel))> <add-indices> args  => kernel_arg_funcs
  ;  <flatten-list> [kernel_arg_funcs]
 <+  ![]
}

// Add an argument to an OpenCL kernel object via call to setKernelArg
//
// TODO-FIXME: need to check procedure interface for argument type if arg is an expression
opencl-build-set-kernel-arg(|ct,kernel) =
     ?(index, VarRef(arg))                                    // scalar variables
  ;  !AssignmentStmt(no-label(),VarDef("cl_status__")
         , FunctionReference("setKernelArg"
              ,[kernel,<subti>(index,1),arg]))
 <+  ?(index, PartRef(arg,_,selector))                        // array variables
  ;  <opencl-kernel-arg-type(|ct)> <hashtable-get(|arg)> ct
  ;  !AssignmentStmt(no-label(),VarDef("cl_status__")
         , FunctionReference("setKernelArg"
              ,[kernel,<subti>(index,1),<id>]))
 <+  ?(index, expr)                                            // assume int scalar
  ;  !AssignmentStmt(no-label(),VarDef("focl_intvar__"), expr)
  ;  <concat> [[<id>],
               [AssignmentStmt(no-label(),VarDef("cl_status__")
                   , FunctionReference("setKernelArg"
                       ,[kernel,<subti>(index,1),VarRef("focl_intvar__")]))] ]

opencl-kernel-arg-type(|ct) =
     ?(cl_symbol, DerivedType("CLBuffer",_),_)
  ;  !clMemObject(cl_symbol)


// Utility strategies
// ------------------

cl-wrap-name =
     is-string
  ;  <conc-strings>("cl_", <id>, "_")
 +>  ?VarRef(name) 
  ;  debug(!"CL_WRAP_NAME: ERROR isVarRef ------------------------------------------------")
  ;  <conc-strings>("cl_", name, "_")

cl-wrap-var =
     ?VarRef(name) 
  ;  <conc-strings>("cl_", name, "_")
 +>  ?VarDef(name) 
  ;  <conc-strings>("cl_", name, "_")
 +>  is-string
  ;  debug(!"CL_WRAP_VAR: ERROR is-string ------------------------------------------------")
  ;  <conc-strings>("cl_", <id>, "_")

ofp-ident
  :  Var(ident,_,_) -> ident

// add subimage device
//
ppm-add-subimage-symbol(|ct,lhs) =
     ?PartRef("get_subimage",args,no-image-selector())
  ;  <hashtable-put(|<fast-var-name>lhs, (<cl-wrap-var>lhs,DerivedType("CLDevice",[]),[]))> ct
  ;  !PartRef("get_subimage",args,no-image-selector())

// Add a symbol to the symbol table
st-add-symbol(|st,type,attrs) =
     ?var
  ;  <hashtable-put(|<ofp-ident>var,(type,attrs))> st

// Add a symbol to the CAFe symbol table
ct-add-symbol(|ct,type,attrs) =
     ?symbol
  ;  <hashtable-put(|symbol,(<cl-wrap-name>symbol,type,attrs))> ct

ct-add-type-decl(|ct) =
     ?ident
  ;  <hashtable-get(|ident)> ct
  ;  ?(clident,type,attrs)
  ;  <fast-build-type-decl(|type,attrs)> clident


// Build symbol table
// ------------------

// Add declared variables to symbol table
//fast-get-symbols(|st) =
//     ?TypeDeclarationStmt(label,type,attrs,vars)
//  ;  <map(st-add-symbol(|st,type,attrs))> vars
//  ;  !TypeDeclarationStmt(label,type,attrs,vars)

// Update symbol table with information from assignment statements
fast-get-symbols(|st) =
     ?AssignmentStmt(label,VarDef(name),lhs)
  ;  <hashtable-put(|name,"dimension(nssq)")> st
; <hashtable-getlist> st
;debug(!"-----assign---------")
  ;  !AssignmentStmt(label,VarDef(name),lhs)

// Get symbols specifically relevant to PPM
// ----------------------------------------

ppm-get-symbols(|st,ct) =
     ?AssignmentStmt(label,lhs,rhs)
//  ;  <ppm-add-subimage-symbol(|ct,lhs)> rhs
  ;  !AssignmentStmt(label,lhs,rhs)


// Add PPM variables to declarations
// ---------------------------------

// Replace get_subimage call with ForOpenCl counterpart
//  - also create kernel calls here
//     - TODO: fix for multiple devices
ppm-add-cl(|st,ct) =
     ?AssignmentStmt(label,VarDef(lhs),PartRef("get_subimage",[arg],_))
  ;  <flatten-list> <map(opencl-build-create-kernel(|ct,lhs))> <hashtable-keys> ct
  ;  <concat> [[AssignmentStmt(label,VarDef(lhs),FunctionReference("get_subimage",
                                      [arg,<cl-wrap-name>lhs]))], <id>]

// Add variables associated with ForOpenCL calls
//    focl_intvar__, cl_status__, cl_size__, ch_ng__(3)
ppm-add-cl(|st,ct) =
     ?Scope(decls,execs,funcs)
  ;  <concat> [[<fast-build-use-stmt>"ForOpenCL"], decls]
  ;  <concat> [<id>, <map(ct-add-type-decl(|ct))> <hashtable-keys()> ct]
  ;  <concat> [<id>, [<fast-build-type-decl(|
                          INTEGER(no-kind()),[])> "focl_intvar__"]]
  ;  <concat> [<id>, [<fast-build-type-decl(|
                          INTEGER(Kind(VarRef("cl_int"  ))),[])> "cl_status__"]]
  ;  <concat> [<id>, [<fast-build-type-decl(|
                          INTEGER(Kind(VarRef("c_size_t"))),[])> "cl_size__"]]
  ;  <concat> [<id>, [TypeDeclarationStmt(no-label()
                        , INTEGER(Kind(VarRef("c_size_t")))
                        , []
                        , [Var("cl_gwo__",
                             ArrayType(INTEGER(Kind(VarRef("c_size_t")))
                                     , [Range(no-lower-bound(), IntVal("3"))],[]), no-init())]
                        )]]
  ;  <concat> [<id>, [TypeDeclarationStmt(no-label()
                        , INTEGER(Kind(VarRef("c_size_t")))
                        , []
                        , [Var("cl_gws__",
                             ArrayType(INTEGER(Kind(VarRef("c_size_t")))
                                     , [Range(no-lower-bound(), IntVal("3"))],[]), no-init())]
                        )]]
  ;  <concat> [<id>, [TypeDeclarationStmt(no-label()
                        , INTEGER(Kind(VarRef("c_size_t")))
                        , []
                        , [Var("cl_lws__",
                             ArrayType(INTEGER(Kind(VarRef("c_size_t")))
                                     , [Range(no-lower-bound(), IntVal("3"))],[])
                                          , Init(ArrayConstructor(AcSpec(no-type-spec()
                                                                         ,<opencl-local-size>))))]
                        )]]
  ;  !Scope(<id>,execs,funcs)

// Replace assignment statements with memory transfer with read/writeBuffer
ppm-add-cl(|st,ct) =
     ?AssignmentStmt(label,PartRef(var,no-section-subscripts(),[VarRef(device)]),VarRef(obj))
  ;  <fast-allocation-size(|st)> var
  ;  ![  AssignmentStmt(no-label(),VarDef("cl_size__"),<id>)
       , AssignmentStmt(no-label(),VarDef("cl_status__")
         , FunctionReference("writeBuffer"
            ,[<cl-wrap-name>var,<conc-strings>("C_LOC(",obj,")"),VarRef("cl_size__")]))
      ]
 +>  ?AssignmentStmt(label,VarDef(var),PartRef(obj,no-section-subscripts(),[VarRef(device)]))
  ;  <fast-allocation-size(|st)> obj
  ;  ![  AssignmentStmt(no-label(),VarDef("cl_size__"),<id>)
       , AssignmentStmt(no-label(),VarDef("cl_status__")
         , FunctionReference("readBuffer"
            ,[<cl-wrap-name>obj,<conc-strings>("C_LOC(",var,")"),VarRef("cl_size__")]))
      ]

// Return the name of the call statement
fast-call-stmt-name =
     ?CallStmt(label, name, args)
   ; !name

// Lower CAF components to serial
// ------------------------------
caf-lower-ast =
     ?Codimension(_)
  ;  ![]

caf-lower-ast =
     ?EntityDecl(name,array_spec,coarray_spec,char_length,init)
  ;  !EntityDecl(name,array_spec,          [],char_length,init)

// This is just used to flatten the attribute list after removing Codimension attribute
caf-lower-ast =
     ?TypeDeclarationStmt(label,type,attrs,vars)
  ;  !TypeDeclarationStmt(label,type,<flatten-list>attrs,vars)

caf-lower-ast =
     ?Allocation(var,shape,coshape)
  ;  !Allocation(var,shape,     [])
